<!DOCTYPE html>
<html>
<head>
<title>Exercise: rewrite a library with <code>k8s-libsonnet</code></title>
<meta charset="utf-8">
<meta name="viewport" content="width=device-width, initial-scale=1.0">
<meta name="generator" content="md2html">
</head>
<body>
<p>[<a href="index.html">index</a>]</p>
<h1>Exercise: rewrite a library with <code>k8s-libsonnet</code></h1>
<p>In the first lesson we've written a extensible library and in the second lesson we've
covered package management with jsonnet-bundler. In this lesson we'll combine what
we've learned and rewrite that library.</p>
<h2>Objectives</h2>
<ul>
<li>Rewrite a library</li>
<li>Vendor and use <code>k8s-libsonnet</code></li>
<li>Understand the <code>lib/k.libsonnet</code> convention</li>
</ul>
<h2>Lesson</h2>
<p>In <a href="lesson1.html">Write an extensible library</a>, we created this library:</p>
<pre><code class="language-jsonnet">local webserver = {
  new(name, replicas=1): {
    local base = self,

    container:: {
      name: 'httpd',
      image: 'httpd:2.4',
    },

    deployment: {
      apiVersion: 'apps/v1',
      kind: 'Deployment',
      metadata: {
        name: name,
      },
      spec: {
        replicas: replicas,
        template: {
          spec: {
            containers: [
              base.container,
            ],
          },
        },
      },
    },
  },

  withImage(image): {
    container+: { image: image },
  },
};

webserver.new('wonderful-webserver')
+ webserver.withImage('httpd:2.5')

// example1.jsonnet
</code></pre>
<p>This library is quite verbose as the author has to provide the <code>apiVersion</code>, <code>kind</code> and
other attributes.</p>
<p>To simplify this, the community has created a Kubernetes client library for Jsonnet called
<a href="https://github.com/jsonnet-libs/k8s-libsonnet"><code>k8s-libsonnet</code></a>. By leveraging this
client library, the author can provide an abstraction that can work across most Kubernetes
versions.</p>
<p>Now go ahead with the <code>k8s-libsonnet</code> library and work out on your own with the resources
in these lessons:</p>
<ol>
<li><a href="lesson1.html">Write an extensible library</a></li>
<li><a href="lesson2.html">Package management</a></li>
</ol>
<p>Find the steps to a solution below.</p>
<h3>Solution</h3>
<blockquote>
<p>Examples below expect to have an environment with <code>export JSONNET_PATH=&quot;lib/:vendor/&quot;</code></p>
</blockquote>
<p>Let's install <code>k8s-libsonnet</code> with jsonnet-bundler and import it:</p>
<p><code>$ jb install https://github.com/jsonnet-libs/k8s-libsonnet/1.23@main</code></p>
<p>Note the alternative naming pattern ending on <code>1.23</code>, referencing the Kubernetes version
this was generated for.</p>
<pre><code class="language-jsonnet">(import 'github.com/jsonnet-libs/k8s-libsonnet/1.23/main.libsonnet')

// example2/lib/k.libsonnet
</code></pre>
<p>The most common convention to work with this is to provide <code>lib/k.libsonnet</code> as
a shortcut.</p>
<pre><code class="language-jsonnet">local k = import 'k.libsonnet';

k.core.v1.container.new('container-name', 'container-image')

// example2/example1.jsonnet
</code></pre>
<p>Many libraries have a line <code>local k = import 'k.libsonnet'</code> to refer to this
library.</p>
<hr>
<p>Let's rewrite the container following the
<a href="https://jsonnet-libs.github.io/k8s-libsonnet/1.23/core/v1/container/">documentation</a>:</p>
<pre><code class="language-jsonnet">local k = import 'k.libsonnet';

local webserver = {
  new(name, replicas=1): {
    local base = self,

    container::
      k.core.v1.container.new('httpd', 'httpd:2.4'),

    deployment: {
      apiVersion: 'apps/v1',
      kind: 'Deployment',
      metadata: {
        name: name,
      },
      spec: {
        replicas: replicas,
        template: {
          spec: {
            containers: [
              base.container,
            ],
          },
        },
      },
    },
  },

  withImage(image): {
    container+:
      k.core.v1.container.withImage(image),
  },
};

webserver.new('wonderful-webserver')
+ webserver.withImage('httpd:2.5')

// example2/example2.jsonnet
</code></pre>
<p>The library has grouped a number of functions under <code>k.core.v1.container</code>, we'll use the
<code>new(name, image)</code> function here, this makes it concise. Additionally the <code>withImage()</code> function uses the function with the same name in the library.</p>
<hr>
<p>And now for the <a href="https://jsonnet-libs.github.io/k8s-libsonnet/1.23/apps/v1/deployment/">deployment</a>:</p>
<pre><code class="language-jsonnet">local k = import 'k.libsonnet';

local webserver = {
  new(name, replicas=1): {
    container::
      k.core.v1.container.new('httpd', 'httpd:2.4'),

    deployment:
      k.apps.v1.deployment.new(
        name,
        replicas,
        [self.container]
      ),
  },

  withImage(image): {
    container+:
      k.core.v1.container.withImage(image),
  },
};

webserver.new('wonderful-webserver')
+ webserver.withImage('httpd:2.5')

// example2/example3.jsonnet
</code></pre>
<p>The <code>new(name, replicas, images)</code> function makes things even more concise. The <code>new()</code>
function is actually a custom shortcut with the most common parameters for a deployment.</p>
<p>Note that we've removed <code>local base = self,</code>, this was not longer needed as the reference
to <code>self.container</code> can now be made inside the same object.</p>
<hr>
<p>Having the library and execution together is not so useful, let's move it into a separate
library and import it again.</p>
<pre><code class="language-jsonnet">local k = import 'k.libsonnet';

{
  new(name, replicas=1): {
    container::
      k.core.v1.container.new('httpd', 'httpd:2.4'),

    deployment:
      k.apps.v1.deployment.new(
        name,
        replicas,
        [self.container]
      ),
  },

  withImage(image): {
    container+:
      k.core.v1.container.withImage(image),
  },
}

// example2/lib/webserver/main.libsonnet
</code></pre>
<p>This removes the <code>local webserver</code> and moves the contents to the root of the file.</p>
<hr>
<pre><code class="language-jsonnet">local webserver = import 'webserver/main.libsonnet';

webserver.new('wonderful-webserver')
+ webserver.withImage('httpd:2.5')

// example2/example4.jsonnet
</code></pre>
<p>If we now <code>import</code> the library, we can access its functions just like before.</p>
<hr>
<pre><code class="language-jsonnet">local webserver = import 'webserver/main.libsonnet';

{
  webserver1:
    webserver.new('wonderful-webserver')
    + webserver.withImage('httpd:2.3'),

  webserver2:
    webserver.new('marvellous-webserver'),

  webserver3:
    webserver.new('incredible-webserver', 2),
}

// example2/example5.jsonnet
</code></pre>
<p>Or, if we want more instances, we can simply do so.</p>
<h2>Conclusion</h2>
<p>TODO</p>
<style>
html {
    margin: 0;
    padding: 0;
    min-height: 100%;
    background: lightgrey;
}

body {
    margin: 0;
    padding: 1.2em;
    min-height: 100%;
    margin-left: 15%;
    margin-right: 15%;
    background: white;
    border: thin solid grey;
    border-top: 0;
    /* sans-serif fonts are generally better readable for people with dyslexia */
    font-family: sans-serif;
    font-size: 1.15rem;
}

blockquote {
    background: lightyellow;
    padding: .1em;
    padding-left: 1em;
    margin-left: 0;
    margin-right: 0;
    display: flow-root;
    font-size: 1rem;
}

p {
    display: flow-root;
    line-height: 1.5em;
}

h1 a, h2 a, h3 a,
h4 a, h5 a, h6 a {
    text-decoration: none;
    color: black;
}

a.anchor-link {
    font-size: 0.9rem;
    vertical-align: middle;
    line-height: 1;
    margin: 0.2em;
    margin-left: -1.2em;
}

h1, h2, h3, h4, h5, h6, hr {
    clear: both;
}

hr {
    visibility: hidden;
}

ol, ul {
    display: flow-root;
}

ol li,
ul li {
    line-height: 1.5em;
}

ul li code, p code {
    background: ghostwhite;
    padding: 0.2em;
}

pre ol {
    display: block;
}

pre {
    float: left;
    margin-top: 0;
    margin-right: 1em;
    width: min(50%, 70ch);
    overflow-x: scroll;
}

@media only screen and (max-width: 720px) {
    body {
        width: auto;
        margin-left: 0;
        margin-right: 0;
        border: 0;
    }
    pre {
        clear: both;
        float: none;
        width: min(95%, 70ch);
    }
}

li.L0, li.L1, li.L2, li.L3,
li.L5, li.L6, li.L7, li.L8 {
    list-style-type: decimal !important;
    line-height: normal;
}

</style>
<script src="https://cdn.jsdelivr.net/gh/google/code-prettify@master/loader/run_prettify.js"></script>
<script>
var pres = document.getElementsByTagName('pre');
for (i=0;i<pres.length; i++) {
    pres[i].className='prettyprint linenums';
}
PR.prettyPrint();

// Source: https://attacomsian.com/blog/deep-anchor-links-javascript
document.querySelectorAll('h2, h3, h4, h5, h6').forEach($heading => {
    //create id from heading text
    var id = $heading.getAttribute("id") || $heading.innerText.toLowerCase().replace(/[`~!@#$%^&*()|+\-=?;:'",.<>\{\}\[\]\\\/]/gi, '').replace(/ +/g, '-');

    //add id to heading
    $heading.setAttribute('id', id);

    //append parent class to heading
    $heading.classList.add('anchor-heading');

    //create anchor
    $anchor = document.createElement('a');
    $anchor.className = 'anchor-link';
    $anchor.href = '#' + id;
    $anchor.innerHTML = '☍';

    //prepend anchor before heading text
    var text = $heading.innerText;
    $heading.innerText = "";
    $heading.appendChild($anchor);
    $heading.append(text);
});

var heading = document.querySelector('h1')

//create anchor
var anchor = document.createElement('a');
anchor.className = 'anchor-link';
anchor.innerHTML = '☰';
anchor.style.cursor = 'pointer';

//prepend anchor before heading text
var text = heading.innerText;
heading.innerText = "";
heading.appendChild(anchor);
heading.append(text);

var toc = document.createElement('div');
toc.id = 'toc';
toc.style.position = 'absolute';
toc.style.top = anchor.offsetTop + 'px';
toc.style.left = anchor.offsetLeft + 'px';
toc.style.background = 'white';
toc.style.border = '1px solid grey';
toc.style.display = 'none';

toc.addEventListener('mouseleave', e => {
    toc.style.display = 'none';
})
anchor.addEventListener('click', e => {
    toc.style.display = 'block';
})

document.querySelectorAll('h2, h3, h4, h5, h6').forEach($heading => {
    var level = $heading.nodeName.charAt(1);
    var current = toc;
    var ul
    var li;
    for (i=1; i<level; i++) {
      ul = current.querySelector('ul:last-child');
      if (ul == null) {
        ul = document.createElement('ul');
        current.appendChild(ul);
      } else {
        li = ul.querySelector('li:last-child');
      }
      current = li
    }

    var li = document.createElement('li');
    ul.appendChild(li)

    var headerId = $heading.id
    var a = document.createElement('a');
    a.href = '#' + headerId;
    var h = $heading;
    a.innerHTML = $heading.innerHTML;
    a.querySelectorAll('a').forEach($child => {
      a.removeChild($child);
    });
    li.appendChild(a)
})

document.querySelector('body').appendChild(toc);

</script>
</body>
</html>
